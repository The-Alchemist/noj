
---
format:
  html:
    toc: true
    toc-depth: 4
    theme: [spacelab, notebooks/custom.scss]
    output-file: interactions_ols.html
code-block-background: true
include-in-header: {text: '<link rel = "icon" href = "data:," />'}
fontsize: 0.9em

---
<style></style><style>.printedClojure .sourceCode {
  background-color: transparent;
  border-style: none;
}
</style>
<script src="interactions_ols_files/md-default0.js" type="text/javascript"></script><script src="interactions_ols_files/md-default1.js" type="text/javascript"></script>

::: {.sourceClojure}
```clojure
(ns interactions-ols
  (:require [tablecloth.api :as tc]
            [tablecloth.pipeline :as tcpipe]
            [fastmath.stats :as fmstats]
            [tech.v3.datatype.functional :as fun]
            [scicloj.metamorph.core :as mm]
            [scicloj.metamorph.ml :as ml]
            [scicloj.metamorph.ml.loss :as loss]
            [scicloj.ml.smile.regression]
            [tech.v3.dataset.modelling :as modelling]
            [tech.v3.dataset.metamorph :as tmd.mm]
            [scicloj.kindly.v4.api :as kindly]
            [scicloj.kindly.v4.kind :as kind]))
```
:::


This examples shows how to do interactions in linear regression with `metamorph.ml`.

Taking ideas from: [Interaction Effect in Multiple Regression: Essentials](http://www.sthda.com/english/articles/40-regression-analysis/164-interaction-effect-in-multiple-regression-essentials/) by Alboukadel Kassambara

First we load the data:


::: {.sourceClojure}
```clojure
(def marketing
  (tc/dataset "https://github.com/scicloj/datarium-CSV/raw/main/data/marketing.csv.gz"
              {:key-fn keyword}))
```
:::


and do some preprocessing to set up the regression:


::: {.sourceClojure}
```clojure
(def preprocessed-data
  (-> marketing
      (tc/drop-columns [:newspaper])
      (modelling/set-inference-target :sales)))
```
:::


## Additive model

First we build an additive model, which model equation is
$$sales = b0 + b1 * youtube + b2 * facebook$$


::: {.sourceClojure}
```clojure
(def additive-pipeline
  (mm/pipeline
   {:metamorph/id :model}
   (ml/model {:model-type :smile.regression/ordinary-least-square})))
```
:::


We evaluate it, 


::: {.sourceClojure}
```clojure
(def evaluations
  (ml/evaluate-pipelines
   [additive-pipeline]
   (tc/split->seq preprocessed-data :holdout)
   loss/rmse
   :loss
   {:other-metrices [{:name :r2
                      :metric-fn fmstats/r2-determination}]}))
```
:::


and print the result:


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :fit-ctx :model ml/thaw-model)
```
:::



::: {.printedClojure}
```clojure
Linear Model:

Residuals:
       Min          1Q      Median          3Q         Max
  -10.7111     -1.0363      0.3926      1.4664      3.7721

Coefficients:
                  Estimate Std. Error    t value   Pr(>|t|)
Intercept           3.1931     0.4531     7.0479     0.0000 ***
youtube             0.0450     0.0018    25.6259     0.0000 ***
facebook            0.1978     0.0100    19.7013     0.0000 ***
---------------------------------------------------------------------
Significance codes:  0 '***' 0.001 '**' 0.01 '*' 0.05 '.' 0.1 ' ' 1

Residual standard error: 2.0922 on 130 degrees of freedom
Multiple R-squared: 0.8861,    Adjusted R-squared: 0.8844
F-statistic: 505.8758 on 3 and 130 DF,  p-value: 4.615e-62

```
:::


We have the following metrics:

$RMSE$


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :test-transform :metric)
```
:::



::: {.printedClojure}
```clojure
1.915771411261115

```
:::


$R^2$


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :test-transform :other-metrices first :metric)
```
:::



::: {.printedClojure}
```clojure
0.9169025322742897

```
:::


## Interaction effects

Now we add interaction effects to it, resulting in this model equation:
$$sales = b0 + b1 * youtube + b2 * facebook + b3 * (youtube * facebook)$$


::: {.sourceClojure}
```clojure
(def pipe-interaction
  (mm/pipeline
   (tcpipe/add-column :youtube*facebook (fn [ds] (fun/* (ds :youtube) (ds :facebook))))
   {:metamorph/id :model}(ml/model {:model-type :smile.regression/ordinary-least-square})))
```
:::


Again we evaluate the model,


::: {.sourceClojure}
```clojure
(def evaluations
  (ml/evaluate-pipelines
   [pipe-interaction]
   (tc/split->seq preprocessed-data :holdout)
   loss/rmse
   :loss
   {:other-metrices [{:name :r2
                      :metric-fn fmstats/r2-determination}]}))
```
:::


and print it and the performance metrices:


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :fit-ctx :model ml/thaw-model)
```
:::



::: {.printedClojure}
```clojure
Linear Model:

Residuals:
       Min          1Q      Median          3Q         Max
   -7.5484     -0.4552      0.1806      0.7091      1.9892

Coefficients:
                  Estimate Std. Error    t value   Pr(>|t|)
Intercept           8.0777     0.3639    22.1983     0.0000 ***
youtube             0.0201     0.0019    10.5209     0.0000 ***
facebook            0.0282     0.0113     2.4841     0.0143 *
youtube*facebook     0.0009     0.0001    15.8560     0.0000 ***
---------------------------------------------------------------------
Significance codes:  0 '***' 0.001 '**' 0.01 '*' 0.05 '.' 0.1 ' ' 1

Residual standard error: 1.1637 on 129 degrees of freedom
Multiple R-squared: 0.9673,    Adjusted R-squared: 0.9666
F-statistic: 1273.0438 on 4 and 129 DF,  p-value: 1.313e-95

```
:::


As the multiplcation of `youtube*facebook` is as well statistically relevant, it
suggests that there is indeed an interaction between these 2 predictor variables youtube and facebook.

$RMSE$


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :test-transform :metric)
```
:::



::: {.printedClojure}
```clojure
1.0868707382818341

```
:::


$R^2$


::: {.sourceClojure}
```clojure
(-> evaluations flatten first :test-transform :other-metrices first :metric)
```
:::



::: {.printedClojure}
```clojure
0.9686613342292784

```
:::


$RMSE$ and $R^2$ of the intercation model are sligtly better.

These results suggest that the model with the interaction term is better than the model that contains only main effects.
So, for this specific data, we should go for the model with the interaction model.



```{=html}
<div style="background-color:grey;height:2px;width:100%;"></div>
```



```{=html}
<div><pre><small><small>source: <a href="https://github.com/scicloj/noj/blob/main/notebooks/interactions_ols.clj">notebooks/interactions_ols.clj</a></small></small></pre></div>
```
